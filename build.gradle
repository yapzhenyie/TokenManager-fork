import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'me.realized'
version '3.3.0'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDir = file("$rootDir/out/")
}

processResources {
    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded"

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [NAME: project.name, VERSION: project.version, SHADED_GROUP: group])
    }
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'bungeecord-repo'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        name 'mvdw-repo'
        url 'http://repo.mvdw-software.be/content/groups/public/'
    }

    maven {
        name 'clip-repo'
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'bstats-repo'
        url 'http://repo.bstats.org/content/repositories/releases/'
    }

    flatDir {
        dirs "$rootDir/libs/"
    }
}

dependencies {
    compile 'org.projectlombok:lombok:1.18.22'
    compile 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compile 'net.milkbowl.vault:Vault:1.7.3'
    compile 'me.clip:placeholderapi:2.10.9'
    compile (name: 'MVdWPlaceholderAPI') {
        transitive = false
    }

    // shaded dependencies
    compile 'redis.clients:jedis:3.5.1'
    compile 'org.apache.commons:commons-pool2:2.4.2'
    compile 'com.zaxxer:HikariCP:4.0.2'
    compile 'org.slf4j:slf4j-nop:1.7.32'
    compile 'org.bstats:bstats-bukkit:3.0.0'
}

shadowJar {
    baseName = 'TokenManager'
    classifier = null

    dependencies {
        include(dependency('redis.clients:.*'))
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.bstats:.*'))
    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + "slf4j"
    relocate 'org.bstats', group + 'bstats'
}

build.dependsOn(shadowJar)